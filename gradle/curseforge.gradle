apply plugin: "com.matthewprenger.cursegradle"

def releaseLink = "https://github.com/badasintended/wthit/releases/tag/${project.version}"

curseforge {
    if (System.getenv("CURSEFORGE_API") != null) {
        apiKey = System.getenv("CURSEFORGE_API")
        project {
            def cf = project.ext.curseforge
            id = cf.id
            releaseType = cf.type

            changelogType = "html"
            changelog = "<a href=\"${releaseLink}\">${releaseLink}</a>"

            cf.versions.each { addGameVersion it }
            addGameVersion project.name.capitalize()

            mainArtifact(remapJar.archivePath) {
                displayName = "[${project.minecraft}] v${project.version}"
            }

            if (cf.containsKey("relations")) relations {
                def rel = cf.relations

                rel.requires?.each { requiredDependency it }
                rel.includes?.each { embeddedLibrary it }
                rel.optional?.each { optionalDependency it }
                rel.tools?.each { tool it }
                rel.breaks?.each { incompatible it }
            }

            if (cf.containsKey("require")) relations {
                cf.require.each {
                    requiredDependency it
                }
            }

            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
