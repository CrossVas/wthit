apply plugin: "com.matthewprenger.cursegradle"

def releaseLink = "https://github.com/badasintended/wthit/releases/tag/${project.version}"

curseforge {
    if (System.getenv("CURSEFORGE_API")) {
        apiKey = System.getenv("CURSEFORGE_API")
        project {
            id = project.cfProjectId
            releaseType = project.cfReleaseType

            changelogType = "html"
            changelog = "<a href=\"${releaseLink}\">${releaseLink}</a>"

            project.cfMcVersion.split(", ").each {
                addGameVersion it
            }
            addGameVersion project.name.capitalize()

            mainArtifact(remapJar) {
                displayName = "[${project.minecraft}] v${project.version}"
            }

            relations {
                project.cfRequire?.split(", ").each {
                    requiredDependency it
                }

                project.cfOptional?.split(", ").each {
                    optionalDependency it
                }

                project.cfBreak?.split(", ").each {
                    incompatible it
                }
            }

            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
