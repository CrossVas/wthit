afterEvaluate {
    task apiJar(type: Jar) {
        dependsOn remapJar

        classifier "api"

        include "fabric.mod.json"
        include "mcp/mobius/waila/api/**"
        exclude "mcp/mobius/waila/api/impl/**"

        from zipTree(remapJar.archiveFile)
    }

    task apiSourcesJar(type: Jar) {
        dependsOn remapSourcesJar

        classifier "api-sources"

        include "fabric.mod.json"
        include "mcp/mobius/waila/api/**"
        exclude "mcp/mobius/waila/api/impl/**"

        from zipTree(remapSourcesJar.output)
    }

    tasks.build.dependsOn apiJar, apiSourcesJar

    publishing {
        publications {
            runtime(MavenPublication) {
                artifactId = rootProject.archivesBaseName
                version = "${project.name}-${project.version}"
                artifact(remapJar) {
                    classifier null
                }
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }
            }
            api(MavenPublication) {
                artifactId = "${rootProject.archivesBaseName}-api"
                version = "${project.name}-${project.version}"
                artifact(apiJar) {
                    classifier null
                }
                artifact(apiSourcesJar) {
                    classifier "sources"
                }
            }
        }

        repositories {
            maven {
                url "https://gitlab.com/api/v4/projects/25106863/packages/maven"
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.getenv("GITLAB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}